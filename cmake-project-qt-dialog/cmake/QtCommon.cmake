macro(add_project_meta FILES_TO_INCLUDE)
    if (NOT RESOURCE_FOLDER)
        set(RESOURCE_FOLDER res)
    endif()

    if (NOT ICON_NAME)
        set(ICON_NAME AppIcon)
    endif()

    if (APPLE)
        set(ICON_FILE ${RESOURCE_FOLDER}/icon/${ICON_NAME}.icns)
    elseif (WIN32)
        set(ICON_FILE ${RESOURCE_FOLDER}/icon/${ICON_NAME}.ico)
    endif()

    if (WIN32)
        configure_file("${RESOURCE_FOLDER}/windows_metafile.rc.in" "windows_metafile.rc")
        set(RES_FILES "windows_metafile.rc")
        set(CMAKE_RC_COMPILER_INIT windres)
        ENABLE_LANGUAGE(RC)
        SET(CMAKE_RC_COMPILE_OBJECT "<CMAKE_RC_COMPILER> <FLAGS> -O coff <DEFINES> -i <SOURCE> -o <OBJECT>")
    endif()

    if (APPLE)
        set_source_files_properties(${ICON_FILE} PROPERTIES MACOSX_PACKAGE_LOCATION Resources)

        set(MACOSX_BUNDLE_BUNDLE_NAME ${CMAKE_PROJECT_NAME})
        set(MACOSX_BUNDLE_BUNDLE_VERSION ${CMAKE_PROJECT_VERSION})
        set(MACOSX_BUNDLE_LONG_VERSION_STRING ${CMAKE_PROJECT_VERSION})
        set(MACOSX_BUNDLE_SHORT_VERSION_STRING "${CMAKE_PROJECT_VERSION_MAJOR}.${CMAKE_PROJECT_VERSION_MINOR}")
        set(MACOSX_BUNDLE_COPYRIGHT ${COPYRIGHT})
        set(MACOSX_BUNDLE_GUI_IDENTIFIER ${IDENTIFIER})
        set(MACOSX_BUNDLE_ICON_FILE ${ICON_NAME})
    endif()

    if (APPLE)
        set(${FILES_TO_INCLUDE} ${ICON_FILE})
    elseif (WIN32)
        set(${FILES_TO_INCLUDE} ${RES_FILES})
    endif()
endmacro()

macro(init_os_bundle)
    if (APPLE)
        set(OS_BUNDLE MACOSX_BUNDLE)
    elseif (WIN32)
        set(OS_BUNDLE WIN32)
    endif()
endmacro()

macro(init_qt)
    set(CMAKE_AUTOMOC ON)
    set(CMAKE_AUTORCC ON)
    set(CMAKE_AUTOUIC ON)
endmacro()
